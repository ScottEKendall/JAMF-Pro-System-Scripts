#!/bin/zsh
#
# PasswordExpire
# Written by: Scott E. Kendall
#
# Created: 04/18/2024
# Last updated: 03/13/2026
#
# Purpose: Provide user notifications of a password expiration.
#
# 1.0 - Initial Release
# 1.1 - Major code cleanup & documentation
#		 Structured code to be more inline / consistent across all apps
# 1.2 - Remove the MAC_HADWARE_CLASS item as it was misspelled and not used anymore...
# 1.3 - Fixed password age calculation
# 		 Add support for 'on demand' viewing of password
# 1.4 - Changed variable declarations around for better readability
# 1.5 - Code cleanup
#       Added feature to read in defaults file
#       removed unnecessary variables.
#       SD min version is now 2.5.0
#       Fixed typos
# 1.6 - Fixed window layout for Tahoe & SD v3.0
# 1.7 - More comments / fixed code formatting
# 1.8 - Changed JAMF 'policy -trigger' to JAMF 'policy -event'
#
# Expected Parameters: 
# $4 - Password Expiration in Days
# $5 - Show "on demand" viewing (Yes) or script processing (No)

######################################################################################################
#
# Global "Common" variables
#
######################################################################################################

SCRIPT_NAME="PasswordExpire"
export PATH=/usr/bin:/bin:/usr/sbin:/sbin
LOGGED_IN_USER=$( scutil <<< "show State:/Users/ConsoleUser" | awk '/Name :/ && ! /loginwindow/ { print $3 }' )
USER_DIR=$( dscl . -read /Users/${LOGGED_IN_USER} NFSHomeDirectory | awk '{ print $2 }' )
USER_UID=$(id -u "$LOGGED_IN_USER")

FREE_DISK_SPACE=$(($( /usr/sbin/diskutil info / | /usr/bin/grep "Free Space" | /usr/bin/awk '{print $6}' | /usr/bin/cut -c 2- ) / 1024 / 1024 / 1024 ))
MACOS_NAME=$(sw_vers -productName)
MACOS_VERSION=$(sw_vers -productVersion)
MAC_RAM=$(($(sysctl -n hw.memsize) / 1024**3))" GB"
MAC_CPU=$(sysctl -n machdep.cpu.brand_string)

ICON_FILES="/System/Library/CoreServices/CoreTypes.bundle/Contents/Resources/"

# Swift Dialog version requirements

SW_DIALOG="/usr/local/bin/dialog"
MIN_SD_REQUIRED_VERSION="2.5.0"
HOUR=$(date +%H)
case $HOUR in
    0[0-9]|1[0-1]) GREET="morning" ;;
    1[2-7])        GREET="afternoon" ;;
    *)             GREET="evening" ;;
esac
SD_DIALOG_GREETING="Good $GREET"

# Make some temp files

###################################################
#
# App Specific variables (Feel free to change these)
#
###################################################
   
# See if there is a "defaults" file...if so, read in the contents
DEFAULTS_DIR="/Library/Managed Preferences/com.gianteaglescript.defaults.plist"
if [[ -f "$DEFAULTS_DIR" ]]; then
    echo "Found Defaults Files.  Reading in Info"
    SUPPORT_DIR=$(defaults read "$DEFAULTS_DIR" SupportFiles)
    SD_BANNER_IMAGE="${SUPPORT_DIR}$(defaults read "$DEFAULTS_DIR" BannerImage)"
    SPACING=$(defaults read "$DEFAULTS_DIR" BannerPadding)
else
    SUPPORT_DIR="/Library/Application Support/GiantEagle"
    SD_BANNER_IMAGE="${SUPPORT_DIR}/SupportFiles/GE_SD_BannerImage.png"
    SPACING=5 #5 spaces to accommodate for icon offset
fi
BANNER_TEXT_PADDING="${(j::)${(l:$SPACING:: :)}}"

# Log files location

LOG_FILE="${SUPPORT_DIR}/logs/${SCRIPT_NAME}.log"

# Display items (banner / icon)

SD_WINDOW_TITLE="${BANNER_TEXT_PADDING}Password Expiration Notice"
SD_IMAGE_TO_DISPLAY="${SUPPORT_DIR}/SupportFiles/PasswordChange.png"
OVERLAY_ICON="/Applications/Self Service.app"
SD_ICON_FILE=${ICON_FILES}"ToolbarCustomizeIcon.icns"

# Trigger installs for Images & icons

SUPPORT_FILE_INSTALL_POLICY="install_SymFiles"
DIALOG_INSTALL_POLICY="install_SwiftDialog"
SD_IMAGE_POLICY="install_passwordSS"

JSS_FILE="/Library/Managed Preferences/com.gianteagle.jss.plist"
SD_TIMER="240"

##################################################
#
# Passed in variables
# 
#################################################

JAMF_LOGGED_IN_USER=${3:-"$LOGGED_IN_USER"}   # Passed in by JAMF automatically
SD_FIRST_NAME="${(C)JAMF_LOGGED_IN_USER%%.*}"
PASSWORD_EXPIRE_IN_DAYS=$4
PASSWORD_CHECK=${5:-"NO"}                   # On Demand (Yes) or script processing (No)

####################################################################################################
#
# Functions
#
####################################################################################################

function create_log_directory ()
{
    # Ensure that the log directory and the log files exist. If they
    # do not then create them and set the permissions.
    #
    # RETURN: None

    # If the log directory doesn't exist - create it and set the permissions (using zsh parameter expansion to get directory)
    LOG_DIR=${LOG_FILE%/*}
    [[ ! -d "${LOG_DIR}" ]] && /bin/mkdir -p "${LOG_DIR}"
    /bin/chmod 755 "${LOG_DIR}"

    # If the log file does not exist - create it and set the permissions
    [[ ! -f "${LOG_FILE}" ]] && /usr/bin/touch "${LOG_FILE}"
    /bin/chmod 644 "${LOG_FILE}"
}

function logMe () 
{
    # Basic two pronged logging function that will log like this:
    #
    # 20231204 12:00:00: Some message here
    #
    # This function logs both to STDOUT/STDERR and a file
    # The log file is set by the $LOG_FILE variable.
    #
    # RETURN: None
    echo "$(/bin/date '+%Y-%m-%d %H:%M:%S'): ${1}" | tee -a "${LOG_FILE}"
}

function check_swift_dialog_install ()
{
    # Check to make sure that Swift Dialog is installed and functioning correctly
    # Will install process if missing or corrupted
    #
    # RETURN: None

    logMe "Ensuring that swiftDialog version is installed..."
    if [[ ! -x "${SW_DIALOG}" ]]; then
        logMe "Swift Dialog is missing or corrupted - Installing from JAMF"
        install_swift_dialog
        SD_VERSION=$( ${SW_DIALOG} --version)        
    fi

    if ! is-at-least "${MIN_SD_REQUIRED_VERSION}" "${SD_VERSION}"; then
        logMe "Swift Dialog is outdated - Installing version '${MIN_SD_REQUIRED_VERSION}' from JAMF..."
        install_swift_dialog
    else    
        logMe "Swift Dialog is currently running: ${SD_VERSION}"
    fi
}

function install_swift_dialog ()
{
    # Install Swift dialog From JAMF
    # PARMS Expected: DIALOG_INSTALL_POLICY - policy trigger from JAMF
    #
    # RETURN: None

    /usr/local/bin/jamf policy -event ${DIALOG_INSTALL_POLICY}
}

function check_support_files ()
{
    [[ ! -e "${SD_BANNER_IMAGE}" ]] && /usr/local/bin/jamf policy -event ${SUPPORT_FILE_INSTALL_POLICY}
}

function create_infobox_message()
{
	################################
	#
	# Swift Dialog InfoBox message construct
	#
	################################

    SD_INFO_BOX_MSG="## System Info ##<br>"
    SD_INFO_BOX_MSG+="${MAC_CPU}<br>"
    SD_INFO_BOX_MSG+="{serialnumber}<br>"
    SD_INFO_BOX_MSG+="${MAC_RAM} RAM<br>"
    SD_INFO_BOX_MSG+="${FREE_DISK_SPACE}GB Available<br>"
    SD_INFO_BOX_MSG+="{osname} {osversion}<br>"
}

function cleanup_and_exit ()
{
    [[ -f ${JSON_OPTIONS} ]] && /bin/rm -rf ${JSON_OPTIONS}
    [[ -f ${TMP_FILE_STORAGE} ]] && /bin/rm -rf ${TMP_FILE_STORAGE}
    [[ -f ${DIALOG_COMMAND_FILE} ]] && /bin/rm -rf ${DIALOG_COMMAND_FILE}
    exit $1
}

####################################################################################################
#
# Application Specific Functions
#
####################################################################################################

function display_msg ()
{
	MainDialogBody=(
        --message "${SD_DIALOG_GREETING} ${SD_FIRST_NAME}.  ${SD_WELCOME_MSG}"
        --icon "${SD_ICON_PRIMARY}"
        --overlayicon "SF=person.circle.fill,weight=heavy,bgcolor=none,colour=blue,colour2=purple"
        --titlefont shadow=1
        --bannerimage "${SD_BANNER_IMAGE}"
        --bannertitle "${SD_WINDOW_TITLE}"
        --infobox "${SD_INFO_BOX_MSG}"
        --width 840
        --quitkey 0
        --timer "${SD_TIMER}"
        --button1text "OK"
        --ontop
    )
        [[ ! -z "${SD_IMAGE_TO_DISPLAY}" ]] && MainDialogBody+=(--height 540 --image "${SD_IMAGE_TO_DISPLAY}")

    # Show the dialog screen and allow the user to choose

    "${SW_DIALOG}" "${MainDialogBody[@]}" 2>/dev/null
    returnCode=$?
}

function display_notification ()
{
    MainDialogBody=(
        --notification
        --title "Password Notification" 
        --message "${SD_WELCOME_MSG}" 
        --button1text "Change Now" 
        --button1action "https://account.activedirectory.windowsazure.com/ChangePassword.aspx"
    )
	# Show the dialog screen and allow the user to choose

    "${SW_DIALOG}" "${MainDialogBody[@]}" 2>/dev/null
}

function duration_in_days ()
{
    # PURPOSE: Calculate the difference between two dates
    # RETURN: days elapsed
    # EXPECTED: 
    # PARMS: $1 - oldest date 
    #        $2 - newest date
    local start end
    calendar_scandate $1
    start=$REPLY
    calendar_scandate $2
    end=$REPLY
    echo $(( ( end - start ) / ( 24 * 60 * 60 ) ))
}

function get_password_info()
{
    # PURPOSE: Retrieve the age of the user password either by reading in the plist key or getting it from the local login password last changed date
    # EXPECTED: JSS_File - path of the plist file to read from
    # RETURN: Password age (in days)
    declare passwordExpireDate
    declare curUser
    declare passwordAge

    # This will try to extract the Password info from the created Plist files (uses EA)
    passwordExpireDate=$(/usr/libexec/plistbuddy -c "print PasswordLastChanged" $JSS_FILE 2>&1)

    if [[ $passwordExpireDate == *"Does Not Exist"* || -z $passwordExpireDate ]]; then
        # Not populated yet, so fall back to the local login password change
        passwordAge=$(expr $(expr $(date +%s) - $(dscl . read /Users/${LOGGED_IN_USER} | grep -A1 passwordLastSetTime | grep real | awk -F'real>|</real' '{print $2}' | awk -F'.' '{print $1}')) / 86400)
    else
        #found the key, so determine the days based off of that
        passwordAge=$(duration_in_days $passwordExpireDate $(date))
    fi
    passwordAge=$(($PASSWORD_EXPIRE_IN_DAYS - $passwordAge))
    echo ${passwordAge}
}

####################################################################################################
#
# Main Script
#
####################################################################################################
autoload 'calendar_scandate'
autoload 'is-at-least'

check_swift_dialog_install
check_support_files
create_infobox_message
SD_ICON_PRIMARY="/System/Applications/Utilities/Keychain access.app"
if is-at-least "15" "${MACOS_VERSION}"; then    #File location change in Sequoia and higher
    SD_ICON_PRIMARY="/System/Applications/Passwords.app"
fi

# Retrieve the users password ago and display he appropriate dialog box
passwordAge=$(get_password_info)
logMe "INFO: Users password age is: "$passwordAge

if [[ ${passwordAge} -le 8 && ${PASSWORD_CHECK:l} == "no" ]]; then
    SD_WELCOME_MSG="Your are receiving this notice because your password is about to expire within the next ${passwordAge} days.  You can click on the 'Unlock / Reset Network Password...' option in **JAMF Connect** to change your password.  You will receive further notices when your password is about to expire within the next 7 days."
    logMe "INFO: Display prompt for user that password will expire in ${passwordAge} days"
    display_msg
else
    SD_WELCOME_MSG="Your password will expire in ${passwordAge} days."
    logMe "INFO: Display notification for user that password will expire in ${passwordAge} days"
    display_notification
fi
exit 0
